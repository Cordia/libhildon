AC_INIT([Hildon gtk widgets], [1.0.0], [michael.kostrzewa@nokia.com], [hildon])
AC_CANONICAL_SYSTEM

AM_CONFIG_HEADER(config.h)
AC_CONFIG_SRCDIR([src/hildon-window.h])
AC_CONFIG_MACRO_DIR([m4])
AM_MAINTAINER_MODE

# define PACKAGE_VERSION_* variables
AS_VERSION

# set the libtool versioning
AS_LIBTOOL(HILDON_WIDGETS, 0, 0, 0)
AM_INIT_AUTOMAKE

AC_PROG_CC
AM_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_AWK

AC_HEADER_STDC

CFLAGS="$CFLAGS -Wall -Wmissing-prototypes -Wmissing-declarations -Werror -Wno-format"
# -Wno-format due to way translation string are done

GTK_VERSION=2.6.10

PKG_CHECK_MODULES(GTK, gtk+-2.0)
AC_SUBST(GTK_LIBS)
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_VERSION)

# ESD is needed for the hildon-note sounds.

PKG_CHECK_MODULES(ESD, esound)
AC_SUBST(ESD_LIBS)
AC_SUBST(ESD_CFLAGS)

PKG_CHECK_MODULES(GCONF, gconf-2.0 >= 2.6)
AC_SUBST(GCONF_CFLAGS)
AC_SUBST(GCONF_LIBS)

PKG_CHECK_MODULES(LIBMB, libmb >= 1.3)
AC_SUBST(LIBMB_CFLAGS)
AC_SUBST(LIBMB_LIBS)

ALL_LINGUAS="en_GB"
AC_SUBST(ALL_LINGUAS) 
AM_GLIB_GNU_GETTEXT

##################################################
# Check for gtk-doc.
##################################################
#ifdef(GTK_DOC_CHECK, GTK_DOC_CHECK([1.0]))
#GTK_DOC_CHECK([1.0])

#AC_CHECK_PROG(DOXYGEN_FOUND, doxygen, yes, no)
#AM_CONDITIONAL(HAVE_DOXYGEN, [test "$DOXYGEN_FOUND" = "yes"])

localedir=${datadir}/locale

AC_SUBST(localedir)

# Disable rebuild of glib-mkenum -generated source code:
AC_ARG_ENABLE(rebuilds, [  --disable-rebuilds      disable all source autogeneration rules],,enable_rebuilds=yes)

# define a MAINT-like variable REBUILD which is set if Perl
# is found, so autogenerated sources can be rebuilt
AC_CHECK_PROGS(PERL, perl5 perl)

REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
     test -n "$PERL" && \
     $PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 ; then
  REBUILD=
fi
AC_SUBST(REBUILD)

# HILDON_WIDGETS__OBJ_*
# default vars for the examples
# includes all the low-level flags
# FIXME Add esd and gconf flags
HILDON_WIDGETS_OBJ_CFLAGS="\$(CFLAGS) \$(GTK_CFLAGS) -I\$(top_builddir)/src/"
HILDON_WIDGETS_OBJ_LIBS="\$(top_builddir)/src/libhildon\$(PACKAGE_VERSION_MAJOR).la \$(GTK_LIBS)"
AC_SUBST(HILDON_WIDGETS_OBJ_CFLAGS)
AC_SUBST(HILDON_WIDGETS_OBJ_LIBS)


AC_CONFIG_FILES([Makefile src/Makefile  \
                 pkgconfig/Makefile \
                 examples/Makefile \
                 pkgconfig/hildon.pc])

AC_OUTPUT

echo "
######################################
Configuration:

- Version............: ${VERSION}
- Compiler...........: ${CC}
- gtk-doc support....: ${enable_gtk_doc}
- Api................: ${PACKAGE}-${PACKAGE_VERSION_MAJOR}

######################################

"
